/**
 * This file is part of DirectAssemblee-Android <https://github.com/direct-assemblee/DirectAssemblee-Android>.
 *
 * DirectAssemblee-Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * DirectAssemblee-Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with DirectAssemblee-Android. If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply from: '../gradleScripts/sonar.gradle'
apply from: '../gradleScripts/jacoco.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-kapt'

def BOOLEAN = "boolean"
def STRING = "String"

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "org.ladlb.directassemblee"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 30
        versionName "1.6.2"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        def BASE_URL = "BASE_URL"
        def LOG_ENABLED = "LOG_ENABLED"
        def TRACKING_ENABLED = "TRACKING_ENABLED"
        def CRASH_TRACKER_ENABLED = "CRASH_TRACKER_ENABLED"

        def URL = "\"https://api.direct-assemblee.org/v1/\""

        all {

            buildConfigField BOOLEAN, LOG_ENABLED, "true"
            buildConfigField BOOLEAN, TRACKING_ENABLED, "true"
            buildConfigField BOOLEAN, CRASH_TRACKER_ENABLED, "true"
            buildConfigField STRING, BASE_URL, URL

            signingConfig signingConfigs.debug

            debuggable true

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "app_name", "Direct AssemblÃ©e"

        }

        release {

            buildConfigField BOOLEAN, LOG_ENABLED, "false"

            debuggable false

        }

        debug {

            buildConfigField BOOLEAN, CRASH_TRACKER_ENABLED, "false"

            proguardFile 'proguard-rules-debug.pro'

        }

        travis {

            buildConfigField BOOLEAN, CRASH_TRACKER_ENABLED, "false"

            proguardFile 'proguard-rules-debug.pro'

            ext.enableCrashlytics = false

        }

    }

    signingConfigs {
        debug {
            storeFile file("code-signing/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        travis {
            storeFile file("code-signing/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta3"
    implementation "androidx.lifecycle:lifecycle-runtime:2.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.1.0"
    implementation 'androidx.core:core-ktx:1.1.0'

    implementation "com.google.android.material:material:1.0.0"

    implementation("com.crashlytics.sdk.android:crashlytics:2.10.1@aar") {
        transitive = true
    }

    implementation 'com.github.bumptech.glide:glide:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'

    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'

    implementation "com.squareup.okhttp3:okhttp:4.2.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.2.0"


    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

    implementation "com.patloew.rxlocation:rxlocation:1.0.5"

    implementation "com.google.android.gms:play-services-location:17.0.0"

    implementation "com.google.firebase:firebase-messaging:20.0.1"
    implementation "com.google.firebase:firebase-core:17.2.1"

    implementation "de.hdodenhof:circleimageview:3.0.1"

    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.2.0"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.61"

    implementation 'com.google.dagger:dagger:2.24'
    implementation 'com.google.dagger:dagger-android-support:2.24'

    kapt 'com.google.dagger:dagger-compiler:2.24'
    kapt 'com.google.dagger:dagger-android-processor:2.24'

    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.23.0"
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.1.1'

}

apply plugin: 'com.google.gms.google-services'

android.applicationVariants.all { variant ->
    if (variant.name == 'travis') {
        project.tasks.getByName('process' + variant.name.capitalize() + 'GoogleServices').enabled = false
    }
}