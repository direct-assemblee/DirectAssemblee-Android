/**
 * This file is part of DirectAssemblee-Android <https://github.com/direct-assemblee/DirectAssemblee-Android>.
 *
 * DirectAssemblee-Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * DirectAssemblee-Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with DirectAssemblee-Android. If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply from: '../gradleScripts/sonar.gradle'
apply from: '../gradleScripts/jacoco.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

def BOOLEAN = "boolean"
def STRING = "String"

android {

    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "org.ladlb.directassemblee"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 30
        versionName "1.6.2"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        def BASE_URL = "BASE_URL"
        def LOG_ENABLED = "LOG_ENABLED"
        def TRACKING_ENABLED = "TRACKING_ENABLED"
        def CRASH_TRACKER_ENABLED = "CRASH_TRACKER_ENABLED"

        def URL = "\"https://api.direct-assemblee.org/v1/\""

        all {

            buildConfigField BOOLEAN, LOG_ENABLED, "true"
            buildConfigField BOOLEAN, TRACKING_ENABLED, "true"
            buildConfigField BOOLEAN, CRASH_TRACKER_ENABLED, "true"
            buildConfigField STRING, BASE_URL, URL

            signingConfig signingConfigs.debug

            debuggable true

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "app_name", "Direct AssemblÃ©e"

        }

        release {

            buildConfigField BOOLEAN, LOG_ENABLED, "false"

            debuggable false

        }

        debug {

            buildConfigField BOOLEAN, CRASH_TRACKER_ENABLED, "false"

            proguardFile 'proguard-rules-debug.pro'

        }

        travis {

            buildConfigField BOOLEAN, CRASH_TRACKER_ENABLED, "false"

            proguardFile 'proguard-rules-debug.pro'

            ext.enableCrashlytics = false

            matchingFallbacks = ['debug']

        }

    }

    signingConfigs {
        debug {
            storeFile file("code-signing/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        travis {
            storeFile file("code-signing/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

}

dependencies {

    implementation project(path: ':address')
    implementation project(path: ':analytics')
    implementation project(path: ':core')
    implementation project(path: ':declaration')
    implementation project(path: ':deputy')
    implementation project(path: ':deputyFind')
    implementation project(path: ':location')
    implementation project(path: ':mandate')
    implementation project(path: ':model')
    implementation project(path: ':networkApiDa')
    implementation project(path: ':notification')
    implementation project(path: ':role')
    implementation project(path: ':settings')
    implementation project(path: ':storage')
    implementation project(path: ':synthesis')
    implementation project(path: ':timeline')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'

    testImplementation 'junit:junit:4.13'
    testImplementation "org.mockito:mockito-core:3.5.10"
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9'

}

apply plugin: 'com.google.gms.google-services'

android.applicationVariants.all { variant ->
    if (variant.name == 'travis') {
        project.tasks.getByName('process' + variant.name.capitalize() + 'GoogleServices').enabled = false
    }
}
