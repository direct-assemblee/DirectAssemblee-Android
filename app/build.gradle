/**
 * This file is part of DirectAssemblee-Android <https://github.com/direct-assemblee/DirectAssemblee-Android>.
 *
 * DirectAssemblee-Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * DirectAssemblee-Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with DirectAssemblee-Android. If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply from: '../gradleScripts/sonar.gradle'
apply from: '../gradleScripts/jacoco.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

def BOOLEAN = "boolean"
def STRING = "String"

android {

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId rootProject.applicationId
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        def BASE_URL = "BASE_URL"
        def LOG_ENABLED = "LOG_ENABLED"
        def TRACKING_ENABLED = "TRACKING_ENABLED"
        def CRASH_TRACKER_ENABLED = "CRASH_TRACKER_ENABLED"

        def URL = "\"https://api.direct-assemblee.org/v1/\""

        all {

            buildConfigField BOOLEAN, LOG_ENABLED, "true"
            buildConfigField BOOLEAN, TRACKING_ENABLED, "true"
            buildConfigField BOOLEAN, CRASH_TRACKER_ENABLED, "true"
            buildConfigField STRING, BASE_URL, URL

            signingConfig signingConfigs.debug

            debuggable true

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "app_name", "Direct Assembl√©e"

        }

        release {

            buildConfigField BOOLEAN, LOG_ENABLED, "false"

            debuggable false

        }

        debug {

            buildConfigField BOOLEAN, CRASH_TRACKER_ENABLED, "false"

            proguardFile 'proguard-rules-debug.pro'

        }


    }

    signingConfigs {
        debug {
            storeFile file("code-signing/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$rootProject.androidx_appcompat"
    implementation "androidx.cardview:cardview:$rootProject.androidx_cardview"
    implementation "androidx.recyclerview:recyclerview:$rootProject.androidx_recyclerview"
    implementation "androidx.browser:browser:$rootProject.androidx_browser"
    implementation "androidx.legacy:legacy-preference-v14:$rootProject.androidx_legacy"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.androidx_legacy"
    implementation "androidx.vectordrawable:vectordrawable:$rootProject.androidx_vectordrawable"
    implementation "androidx.browser:browser:$rootProject.androidx_browser"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.androidx_constraintlayout"
    implementation "androidx.lifecycle:lifecycle-runtime:$rootProject.androidx_lifecycle"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.androidx_lifecycle"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$rootProject.androidx_lifecycle"

    implementation "com.google.android.material:material:$rootProject.android_material"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_version"

    implementation("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlytics@aar") {
        transitive = true
    }

    implementation "com.squareup.picasso:picasso:$rootProject.picasso"

    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofit"

    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp3"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp3"

    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroid"

    implementation "com.patloew.rxlocation:rxlocation:$rootProject.rxLocation"

    implementation "com.google.android.gms:play-services-location:$rootProject.androidGms"

    implementation "com.google.firebase:firebase-messaging:$rootProject.firebaseMessaging"
    implementation "com.google.firebase:firebase-core:$rootProject.firebaseCore"

    implementation "de.hdodenhof:circleimageview:$rootProject.circleImageView"

    implementation "com.github.PhilJay:MPAndroidChart:v$rootProject.MPAndroidChart"

    // rx binding
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rootProject.rxbinding"

    testImplementation "junit:junit:$rootProject.junit"
    testImplementation "org.mockito:mockito-core:$rootProject.mockito"
    testImplementation "org.robolectric:robolectric:$rootProject.robolectric"
    testImplementation "org.robolectric:shadows-support-v4:$rootProject.ext.shadowsSupportv4"
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.google.gms.google-services'
